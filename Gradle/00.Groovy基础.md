## Groovy基础

Gradle的DSL是由Groovy实现的，提供了基于Java的语法糖。Gradle的使用者不必成为Groovy的专家才开始写脚本。因为Groovy是在Java基础上实现的，可通过尝试使用Groovy的语言特性来逐渐迁移。  

# 1. 什么是Groovy
Groovy是针对Java虚拟机(JVM)的一种动态编程语言，语法与Java类似，且与已存在的Java类或者类库集成。同时，Gradle借鉴了Ruby、Python等其他语言中的强大的编程特性。Groovy是一种脚本语言，不用编译其他代码。同时，Groovy代码也支持被编译成Java二进制代码。  

# 2. 对Groovy知识的了解程度
Gradle新手只需要学习很少的Groovy知识就能编写出第一个构建脚本。但强烈推荐学习一些Java知识，因Groovy几乎100%兼容Java。在构建脚本中实现task动作的时候，可以选择使用纯Java代码或使用Groovy的具有表述性的语言构造。  

# 3. Java和Groovy语法
在学习Groovy的过程中，在构建中混合使用Java和Groovy可以工作得很好。在Groovy中可以高效地工作，同时编写更少的代码。Groovy针对Java编写的类假定了合理的默认值，尤其是如下优化：  
+ 表达式后的分号是可选的；
+ 每个类、构造器和方法默认是public的；
+ 在Groovy中，方法体的最后一个表达式的值会被作为返回值，return语句是可选的；
+ Groovy编译器会自动加上getting/setting方法；
+ 类的属性可通过点号来获取，在底层Groovy调用的是自动生成的getting/setting方法；
+ 使用==比较两个类的实例，在底层Groovy会自动调用equals()方法，可避免空指针。  

# 4. 高效的Groovy特性

## 4.1 assert语句
Java中的assert关键字用阿里验证代码中的前置或后置条件，仅设置了运行时标志(-ea或-enableassertion)来进行断言检查时才有用，而Groovy的assert语句一直有用。  

```groovy
def version = 12
assert version == 12
version++
assert version == 12
```  

## 4.2 可选类型定义
Groovy并不强制显示声明变量类型、方法参数或返回类型。可以使用`def`关键字进行简单的标识，作为`java.lang.Object`的一个占位符。在运行时，Groovy通过所赋的值分析其类型。   

```groovy
def buildTool = 'Gradle'
assert buildTool.class == java.lang.String
def initProjectVersion(major, minor) {
  new ProjectVersion(major, minor)
}
assert initProjectVersion(1,2).class == com.manning.gia.ProjectVersion
```

## 4.3 可选的括号
在Groovy中如果方法签名需要至少一个参数的话，则方法调用可以省略括号。  

```groovy
initProjectVersion(1,2)
initProjectVersion 1,2

println('Groovy is awesome!')
println 'Groovy is awesome'
```

## 4.4 字符串
在Groovy中有三种不同方式可以定义字符串：  
+ 带单引号的字符串通常创建出等效于Java的String类型；
+ 使用与java一致的双引号创建字符串；
+ 多个字符串使用三个双引号，在赋值长文本或者格式化时非常有用。

## 4.5 Groovy字符串(GString)
在Groovy中带双引号的字符串可以插值到变量或表达式中，通过$和花括号来表示。在运行时，Groovy会计算其中的表达式并组成一个字符串。在Groovy中通常称为GString。  

```groovy
def language = 'groovy'
def sentence = "$language is awesome!"
assert sentence == 'groovy is awesome!'

def improvedSentence = "${language.capitalize()} is awesome!"
assert improvedSentence == 'Groovy is awesome!'
```

## 4.6 集合API
Groovy为集合API的实现提供了简洁的语法，比Java中类似的使用更加方便。  

**List**  
+ 在方括号中放入一串以逗号分隔的值，可以初始化新的List，默认实现是java.util.ArrayList；
+ 添加一些语法糖来简化List的使用，如通过左移向List中添加一个新元素。  

```groovy
def buildTools = ['Ant', 'Maven']
assert buildTools.class == java.util.ArrayList
assert buildTools.size() == 2
assert buildTools[1] == 'Maven'

buildTools << 'Gradle'
assert buildTools.size() == 3
assert buildTools[2] == 'Gradle'

buildTools.each { buildTool ->
  println buildTool
}
```

**Map**  
在方括号中放入一串用逗号分隔的键值对就可以创建一个新的Map，默认实现是java.lang.LinkedHashMap。  

```groovy
def incerptionYears = ["Ant": 2000, "Maven":2004]
assert incerptionYears.getClass() == java.util.LinkedHashMap
assert incerptionYears.size() == 2
assert incerptionYears.Ant == 2000
assert incerptionYears['Ant'] == 2000

incerptionYears['Gradle'] = 2009
assert incerptionYears.size() == 3
assert incerptionYears['Gradle'] == 2009

incerptionYears.each {buildTool, year -> 
	println "$buildTool was first released in $year"
}
```

## 4.7 命名参数
如果没有定义构造器时，Groovy提供了一样方面设置属性值的方式，叫做*命名参数*。这种机制首先调用类的默认构造器，然后为每个参数调用对应的setter方法。  

```groovy
class ProjectVersion {
  Integer major
  Integer minor
}

ProjectVersion projectVersion = new ProjectVersion(major: 1, minor: 10)
assert projectVersion.minor == 10
projectVersion.minor = 30
assert projectVersion.minor == 30
```

## 4.8 闭包
闭包是一个groovy.lang.Closure的代码块，与其他编程语言的lambda特性类似。闭包可以被赋值给变量，作为参数传递给方法，并像普通方法一样来调用。  

#### 隐式的闭包参数
每个没有显示定义任何参数的闭包都可以访问一个隐式的参数it。it代表调用这个闭包的时候第一个传递进来的参数。如果没有提供参数，那么参数的值就是null。  

```groovy
def incrementMajorProjectVersion = {
  it.major++
}

ProjectVersion projectVersion = new ProjectVersion(major:1, minor:10)
incrementMajorProjectVersion(projectVersion)
assert projectVersion.major == 2
```  

#### 显式的闭包参数
除了使用隐式的闭包参数，还可以自定义更具描述性的参数名称。  

+ 单一的、显式参数的闭包  

```groovy
def incrementMajorProjectVersion = { ProjectVersion version -> 
  version.major++
}

ProjectVersion projectVersion = new ProjectVersion(major:1, minor:10)
incrementMajorProjectVersion(projectVersion)
assert projectVersion.major == 2
```
+ 多个无类型参数的闭包  

```groovy
def setFullProjectVersion = { projectVersion, major, minor -> 
  projectVersion.major = major
  projectVersion.minor = minor
}

ProjectVersion projectVersion = new ProjectVersion(major:1, minor:10)
setFullProjectVersion(projectVersion, 2, 1)
assert projectVersion.major == 2
assert projectVersion.minor == 1
```

#### 闭包返回值
闭包总是会返回一个值。返回值是闭包的最后一条语句的值，或者是可执行的return语句的值。如果闭包的最后一条语句没有值，就返回null。  

```groovy
ProjectVersion projectVersion = new ProjectVersion(major:1, minor:10)
def minorVersion = {projectVersion.minor}
assert minorVersion() == 10
```

#### 闭包作为方法参数

```groovy
Integer incrementVersion(Closure closure, Integer count) {
  closure() + count
}

ProjectVersion projectVersion = new ProjectVersion(major: 1, minor: 10)
assert incrementVersion({projectVersion.minor}, 2) == 12
```

#### 闭包委托
闭包代码在委托的闭包上执行。默认的，这个委托就是闭包的所有者。比如，如果在Groovy脚本中定义了一个闭包，那么所有者就是一个groovy.lang.Script实例。闭包的隐式变量delegate允许重定义默认的所有者。  
```groovy
class ProjectVersion {
  Integer major
  Integer minor
  
  void increment(Closure closure) {
    closure.resolveStrategy = Closure.DELEGATE_ONLY
	closure.delegate = this
	closure()
  }
}

ProjectVersion projectVersion = new ProjectVersion(major: 1, minor: 10)
projectVersion.increment {major += 1}
assert projectVersion.major == 2
projectVersion.increment {minor += 5}
assert projectVersion.minor == 15
```

## 4.9 Groovy开发工具库
GDK扩展了JDK，其为标准的JDK类提供了很多方便的方法。  

```groovy
def buildTools = ['Ant', 'Maven', 'Gradle']
assert buildTools.find { it == 'Gradle'} == 'Gradle'
assert buildTools.every { it.size() >= 4} == false

assert 'gradle'.capitalize() == 'Gradle'

new File('build.gradle').eachLine { line ->
  println line
}
```

# 5. 在Gradle构建脚本中使用Groovy
Gradle构建脚本是合法的Groovy脚本。在构建脚本中，可以使用Groovy语言的所有特性。  
Gradle使用Groovy编写的DSL来建模典型的构建关系。每个构建脚本都至少有一个对应的org.gradle.api.Project实例。在大多数情况下，在构造脚本中调用的属性和方法都自动委托给了这个Project实例。  

